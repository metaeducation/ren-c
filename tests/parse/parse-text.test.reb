; %parse-text.test.reb
;
; A TEXT! rule will capture the actual match in a block.  But for a string, it
; will capture the *rule*.

; No-op rules need thought in UPARSE, in terms of NULL/BLANK! behavior.  But
; empty string should be a no-op on string input, and an empty rule should
; always match.
;

; rule capture: efficient (doesn't require a copy if you're not using it)
(
    rule: "cd"
    result: ~
    all [
        "cd" = result: parse "abcd" ["ab" rule]
        "cdef" = append result "ef"
        "cdef" = rule
    ]
)

("" = parse "" [""])

("hello" == parse ["hello"] ["hello"])

("a" == parse "a" ["a"])
("ab" == parse "ab" ["ab"])
("abc" == parse "abc" ["abc"])
("abc" == parse "abc" ["abc" <end>])

; Ren-C does not mandate that rules make progress, so matching empty strings
; works, as it does in Red.
[
    ("ab" == parse "ab" [to [""] "ab"])
    ("ab" == parse "ab" [to ["a"] "ab"])
    ("ab" == parse "ab" [to ["ab"] "ab"])
    ("ab" == parse "ab" [thru [""] "ab"])
    ("b" == parse "ab" [thru ["a"] "b"])
    ("" == parse "ab" [thru ["ab"] ""])
]

[(
    x: ~
    rule: [x: "a"]
    all [
        "a" == parse "a" rule
        same? x second rule
    ]
)(
    x: ~
    rule: [x: "a"]
    data: ["a"]
    all [
        "a" == parse data rule
        same? x first data
    ]
)]

; Multi-byte characters and strings present a lot of challenges.  There should
; be many more tests and philosophies written up of what the semantics are,
; especially when it comes to BINARY! and ANY-STRING? mixtures.  These tests
; are better than nothing...
[
    (
        catchar: #"ğŸ±"
        #ğŸ± == parse #{F09F90B1} [catchar]
    )
    (
        cattext: "ğŸ±"
        "ğŸ±" == parse #{F09F90B1} [cattext]
    )
    ~find-string-binary~ !! (
        catbin: #{F09F90B1}
        parse "ğŸ±" [catbin]
    )
    (
        catchar: #"ğŸ±"
        #ğŸ± == parse "ğŸ±" [catchar]
    )
]

[
    (
        bincat: to-binary -{CğŸ˜ºT}-
        bincat = #{43F09F98BA54}
    )

    ("CğŸ˜ºT" == parse bincat [-{CğŸ˜ºT}-])

    ("cğŸ˜ºt" == parse bincat [-{cğŸ˜ºt}-])

    ~parse-mismatch~ !! (parse:case bincat [-{cğŸ˜ºt}- <end>])
]

(
    test: to-binary -{The CğŸ˜ºT Test}-
    all [
        let x: ~
        #{43F09F98BA54} == parse test [to -{cğŸ˜ºt}- x: across to space to <end>]
        x = #{43F09F98BA54}
        "CğŸ˜ºT" = to-text x
    ]
)

[https://github.com/red/red/issues/678
    ("cat" == parse "catcatcatcat" [repeat 4 "cat"])
    ("cat" == parse "catcatcat" [repeat 3 "cat"])
    ("cat" == parse "catcat" [repeat 2 "cat"])
    ~parse-mismatch~ !! (parse "cat" [repeat 4 "cat"])
    ~parse-mismatch~ !! (parse "cat" [repeat 3 "cat"])
    ~parse-mismatch~ !! (parse "cat" [repeat 2 "cat"])
    ("cat" == parse "cat" [repeat 1 "cat"])
]

; String casing
[
    ("A" == parse "a" ["A"])
    ~parse-mismatch~ !! (parse "a" [#A])
    ~parse-mismatch~ !! (parse:case "a" ["A"])
    ~parse-mismatch~ !! (parse:case "a" [#A])
    ("a" == parse:case "a" ["a"])
    (#a == parse:case "a" [#a])
    ("A" == parse:case "A" ["A"])
    (#A == parse:case "A" [#A])
    ("test" == parse "TeSt" ["test"])
    ~parse-mismatch~ !! (parse:case "TeSt" ["test"])
    ("TeSt" == parse:case "TeSt" ["TeSt"])
]

; Casing in a block
[
    ("a" = parse:case ["a"] ["a"])
    ~parse-mismatch~ !! (parse:case ["a"] ["A"])
]

; String unicode
[
    (#Ã© == parse "abcdÃ©" [#a #b #c #d #Ã©])
    ("abcdÃ©" == parse "abcdÃ©" ["abcdÃ©"])
    ~parse-mismatch~ !! (parse "abcde" [#a #b #c #d #Ã©])
    (#Ã© == parse "abcdÃ©" [#a #b #c #d #Ã©])
    (#"âœ" == parse "abcdÃ©âœ" [#a #b #c #d #Ã© #"âœ"])
    ("abcdÃ©âœ" == parse "abcdÃ©âœ" ["abcdÃ©âœ"])
    ~parse-mismatch~ !! (parse "abcdÃ©" ["abcdÃ©âœ"])
    ~parse-mismatch~ !! (parse "abâœcdÃ©" ["abcdÃ©âœ"])
    ~parse-incomplete~ !! (parse "abcdÃ©âœ" ["abcdÃ©"])
    ("âœabcdÃ©" == parse "âœabcdÃ©" ["âœabcdÃ©"])
    (#"ğ€€" == parse "abcdÃ©âœğ€€" [#a #b #c #d #Ã© #"âœ" #"ğ€€"])
    ("abğ€€cdÃ©âœ" == parse "abğ€€cdÃ©âœ" ["abğ€€cdÃ©âœ"])
    ~parse-mismatch~ !! (parse "abcdÃ©" ["abcğ€€dÃ©"])
    ~parse-mismatch~ !! (parse "ğ€€abcdÃ©" ["ağ€€bcdÃ©"])
    ~parse-incomplete~ !! (parse "abcdÃ©ğ€€" ["abcdÃ©"])
    ("ğ€€abcdÃ©" == parse "ğ€€abcdÃ©" ["ğ€€abcdÃ©"])
]

[
    (
        str: "Lorem ipsum dolor sit amet."
        ok
    )

    (#"." == parse str [thru "amet" one])
    (
        res: ~
        all [
            "dolor" == parse str [
                thru "ipsum" one res: across to #" " to <end>
            ]
            res = "dolor"
        ]
    )
    (
        res: ~
        all [
            "sum dolor sit amet." == parse str [thru #p res: <here> to <end>]
            9 = index? res
        ]
    )
]
